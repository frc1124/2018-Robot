buildscript {
  ext.kotlin_version = "1.1.3"
  ext.gradlerio_version = "2017.2.3"

  repositories {
    jcenter()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "gradle.plugin.jaci.openrio.gradle:GradleRIO:$gradlerio_version"
    classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0"
  }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "jaci.openrio.gradle.GradleRIO"
apply plugin: "org.junit.platform.gradle.plugin"

deploy.dependsOn "build"

repositories {
  jcenter()
  mavenCentral()
  maven { url "http://dl.bintray.com/jetbrains/spek" }
}

wpi {
  wpilibVersion = "+"
  ntcoreVersion = "+"
  opencvVersion = "+"
  cscoreVersion = "+"
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
  compile wpilib()
  compile talonSrx()
  testCompile 'org.jetbrains.spek:spek-api:1.1.4'
  testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.4'
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

junitPlatform {
  filters {
    engines {
      include 'spek'
    }
  }
}

frc {
  team = "9999"
  robotClass = "org.team9999.gamename.Robot.kt"
  rioIP = "10.99.99.89"
}

def robotManifest = {
  attributes "Main-Class": "edu.wpi.first.wpilibj.RobotBase"
  attributes "Robot-Class": frc.robotClass
}

jar {
  from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  manifest robotManifest
}